plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'org.springdoc.openapi-gradle-plugin' version "${springdocOpenapiGradlePluginVersion}"
}

group = 'com.dressca'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation supportDependencies.spring_boot_starter_web
	implementation supportDependencies.spring_boot_starter_validation
	implementation supportDependencies.spring_boot_starter_actuator
	implementation supportDependencies.springdoc_openapi_starter_webmvc_ui
	implementation supportDependencies.commons_lang3
	
	implementation project(':web')
	implementation project(':application-core')
	implementation project(':infrastructure')
	implementation project(':system-common')

	testImplementation supportDependencies.spring_boot_starter_test
	testImplementation supportDependencies.spring_boot_starter_log4j2

	compileOnly supportDependencies.servlet_api
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}
afterEvaluate {
    tasks.named("forkedSpringBootRun") {
		workingDir("$rootDir/api-docs")
	}
}
openApi {
    apiDocsUrl.set("http://localhost:8080/api-docs")
    outputDir.set(file("$rootDir/api-docs"))
    outputFileName.set("api-specification.json")
}
tasks.named('test') {
	useJUnitPlatform()
}

build.dependsOn("generateOpenApiDocs")