plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'org.springdoc.openapi-gradle-plugin' version "${springdocOpenapiGradlePluginVersion}"
}

group = 'com.dressca'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation supportDependencies.spring_boot_starter_web
	implementation supportDependencies.springdoc_openapi_starter_webmvc_ui
	implementation supportDependencies.spring_cloud_azure_starter
	implementation supportDependencies.spring_boot_starter_oauth2_resource_server
	implementation supportDependencies.spring_boot_starter_log4j2

	implementation project(':system-common')

	testImplementation supportDependencies.spring_boot_starter_test
	testImplementation supportDependencies.spring_boot_starter_log4j2
	testImplementation supportDependencies.spring_security_test
}

dependencyManagement {
	imports {
		mavenBom supportDependencies.spring_cloud_azure_dependencies
	}
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}
afterEvaluate {
    tasks.named("forkedSpringBootRun") {
		workingDir("$rootDir/api-docs")
	}
}
openApi {
    apiDocsUrl.set("http://localhost:8080/api-docs")
    outputDir.set(file("$rootDir/api-docs"))
    outputFileName.set("api-specification.json")
}
tasks.named('test') {
	useJUnitPlatform()
}
tasks.register('bootRunDev', org.springframework.boot.gradle.tasks.run.BootRun){
	group = 'application'
	description = 'Runs this project as a Spring Boot Application using local profile.'
	args = ["--spring.profiles.active=local"]
	main = 'com.dressca.web.WebApplication'
	classpath = sourceSets.main.runtimeClasspath
}

build.dependsOn("generateOpenApiDocs")