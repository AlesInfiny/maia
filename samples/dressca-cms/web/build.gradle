plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'org.springdoc.openapi-gradle-plugin' version "${springdocOpenapiGradlePluginVersion}"
}

group = 'com.dressca.cms'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation supportDependencies.spring_boot_starter_web
	implementation supportDependencies.spring_boot_starter_validation
	implementation supportDependencies.spring_boot_starter_actuator
	implementation supportDependencies.springdoc_openapi_starter_webmvc_ui
	implementation supportDependencies.commons_lang3
	implementation supportDependencies.h2database
	implementation supportDependencies.spring_boot_starter_thymeleaf
	implementation supportDependencies.thymeleaf_layout_dialect
	implementation supportDependencies.spring_boot_starter_log4j2

	implementation project(':announcement')
	implementation project(':system-common')

	testImplementation supportDependencies.spring_boot_starter_test
	compileOnly supportDependencies.servlet_api
	developmentOnly supportDependencies.spring_boot_devtools
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

afterEvaluate {
    tasks.named("forkedSpringBootRun") {
		workingDir("$rootDir/api-docs/web")
	}
}

openApi {
    apiDocsUrl.set("http://localhost:8080/api-docs")
    outputDir.set(file("$rootDir/api-docs/web"))
    outputFileName.set("api-specification.json")
}

tasks.register('bootRunDev', org.springframework.boot.gradle.tasks.run.BootRun){
	group = 'application'
	description = 'Runs this project as a Spring Boot Application using local profile.'
	args = ["--spring.profiles.active=local"]
	main = 'com.dressca.cms.web.WebApplication'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.named('test') {
	useJUnitPlatform()
}

build.dependsOn("generateOpenApiDocs")